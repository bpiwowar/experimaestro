description = 'experimaestro server'

apply plugin: 'application'

def generatedResources = "$buildDir/classes/main"

sourceSets {
    main {
        output.resourcesDir = generatedResources
    }
}

dependencies {
    compile project(':experimaestro-helper')

    // Web & JsonRPC server
    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.3.7.v20160115'
    compile group: 'org.eclipse.jetty', name: 'jetty-security', version: '9.3.7.v20160115'
    compile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.3.7.v20160115'
    compile group: 'org.eclipse.jetty.websocket', name: 'websocket-servlet', version: '9.3.7.v20160115'
    compile group: 'org.eclipse.jetty.websocket', name: 'websocket-server', version: '9.3.7.v20160115'

    compile group: 'org.apache.xmlrpc', name: 'xmlrpc-client', version: '3.1.3'
    compile group: 'org.apache.xmlrpc', name: 'xmlrpc-server', version: '3.1.3'

    // Command line
    compile group: 'net.bpiwowar', name: 'argj', version: '1.1.4'

    // Script languages
    compile group: 'org.python', name: 'jython-standalone', version: '2.7.0'
    compile group: 'org.mozilla', name: 'rhino', version: '1.7.7'

    // Database
    compile group: 'org.hsqldb', name: 'hsqldb', version: '2.3.2'
    compile group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.1'

    // SSH / SFTP
    compile group: 'com.github.jnr', name: 'jnr-unixsocket', version: '0.3'
    compile group: 'com.jcraft', name: 'jsch', version: '0.1.52'
    compile group: 'net.bpiwowar', name: 'jsch-nio', version: '0.1.8-SNAPSHOT'

    // General purpose modules
    compile group: 'it.unimi.dsi', name: 'fastutil', version: '6.5.15'
    compile group: 'com.google.guava', name: 'guava', version: '18.0'
    compile group: 'org.apache.commons', name: 'commons-compress', version: '1.4.1'
    compile group: 'commons-configuration', name: 'commons-configuration', version: '1.6'
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    compile group: 'net.sf.saxon', name: 'Saxon-HE', version: '9.5.1-6'

    // Test modules
    testCompile group: 'org.testng', name: 'testng', version: '6.9.4'
    testCompile group: 'org.apache.sshd', name: 'sshd-core', version: '0.14.0'
    testCompile group: 'org.bouncycastle', name: 'bcprov-jdk16', version: '1.46'
    testCompile group: 'com.thetransactioncompany', name: 'jsonrpc2-client', version: '1.15'
}

buildscript {
    dependencies {
        classpath 'org.ajoberstar:gradle-git:1.2.0'
    }
    repositories {
        mavenCentral()
    }
}

import org.ajoberstar.grgit.Grgit

task printHeadHash << {
    def git = Grgit.open(project.file('..'))

    println git.head().id
    println git.head().author
    println git.head().shortMessage
    println git.tag.list()
}

mainClassName = 'bpiwowar.experiments.Run'

ext {
    // Open the Git repository in the main directory.
    git = org.ajoberstar.grgit.Grgit.open(file('..'))
    git_head = git.head()

}

applicationDistribution.from("scripts") {
    into "bin"
}

task gitVersion(type: Copy) {
    from 'src/dist'
    include 'VERSION'
    into 'build/classes/main'
    expand(
            buildDate: new Date(),
            version: project.version,
            appName: applicationName,
            git_commit_id: git_head.id,
            git_dirty: false,
            git_commit_message_short: git_head.shortMessage,
            git_commit_time: git_head.time,
            git_branch: git.branch.current.name,
            git_tags: "",
            git_remote_origin_url: "",

    )
}
compileJava.dependsOn gitVersion


task(server, dependsOn: 'classes', type: JavaExec) {
    main = 'bpiwowar.experiments.Run'
    classpath = sourceSets.main.runtimeClasspath
    args 'server'
}

test.useTestNG()
